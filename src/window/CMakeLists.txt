#
# CMakeLists.txt
# Chris Vig (chris@invictus.so)
#

# -- Project Configuration --

# Library name
set(WINDOW_LIBRARY_NAME oglwindow)

# Library source code
set(WINDOW_LIBRARY_SOURCES
  window.cpp
  impl/glfw_interface.cpp
  impl/glfw_window.cpp)

# Initial configuration
string(TOUPPER ${WINDOW_LIBRARY_NAME} WINDOW_LIBRARY_NAME_UPPER)
list(APPEND WINDOW_LIBRARY_COMPILE_DEFINITIONS -DOGL_LIB${WINDOW_LIBRARY_NAME_UPPER})

# -- Libraries --

# Include GLFW library
pkg_check_modules(GLFW REQUIRED glfw3)
list(APPEND WINDOW_LIBRARY_INCLUDE_DIRECTORIES ${GLFW_INCLUDE_DIRS})
list(APPEND WINDOW_LIBRARY_COMPILE_OPTIONS ${GLFW_CFLAGS_OTHER})
list(APPEND WINDOW_LIBRARY_LINK_DIRECTORIES ${GLFW_LIBRARY_DIRS})
list(APPEND WINDOW_LIBRARY_LINK_LIBRARIES ${GLFW_LIBRARIES})

# -- Target Definition --

add_library(${WINDOW_LIBRARY_NAME} STATIC
  ${WINDOW_LIBRARY_SOURCES})
target_compile_definitions(${WINDOW_LIBRARY_NAME}
  PRIVATE ${WINDOW_LIBRARY_COMPILE_DEFINITIONS})
target_compile_options(${WINDOW_LIBRARY_NAME}
  PRIVATE ${WINDOW_LIBRARY_COMPILE_OPTIONS})
target_link_libraries(${WINDOW_LIBRARY_NAME}
  PUBLIC ${WINDOW_LIBRARY_LINK_LIBRARIES})

# -- Exports --

# Library name
set(WINDOW_LIBRARY_NAME ${WINDOW_LIBRARY_NAME} PARENT_SCOPE)

# Link directories
list(APPEND APP_EXECUTABLE_LINK_DIRECTORIES ${WINDOW_LIBRARY_LINK_DIRECTORIES})
set(APP_EXECUTABLE_LINK_DIRECTORIES ${APP_EXECUTABLE_LINK_DIRECTORIES} PARENT_SCOPE)
